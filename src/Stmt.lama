-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun eval (c, stmt) {
  case stmt of
    Assn  (x, e)   -> [ getState (c) <- [ x, evalExpr (getState (c), e) ], getWorld (c) ]
  | Seq   (s1, s2) -> eval (eval (c, s1), s2)
  | Skip           -> c
  | Read  (x)      -> case readWorld (getWorld (c)) of
                        [v, w] -> [ getState (c) <- [ x, v ], w ]
                      esac
  | Write (e)      -> [ getState (c), writeWorld (evalExpr (getState (c), e), getWorld (c)) ]
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}

fun getState (c) {
  c.fst
}

fun getWorld (c) {
  c.snd
}
